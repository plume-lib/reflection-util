buildscript {
  repositories {
    mavenLocal()
  }

  dependencies {
    classpath 'com.jaredsburrows:gradle-checker-framework-plugin:0.2.3-SNAPSHOT'
  }
}

plugins {
  id 'java-library'

  // To create a fat jar build/libs/reflection-util-all.jar, run: ./gradlew shadowJar
  id "com.github.johnrengelman.shadow" version "2.0.4"
  // Run the Error Prone linter.
  id "net.ltgt.errorprone-base" version "0.0.16"
  // Formating is disabled due to poor handling of type annotations; wait until
  // this issue is fixed: https://github.com/google/google-java-format/issues/5
  // // Code formatting with Google Java Format; defines targets "goJF" and "verJF".
  // id "com.github.sherter.google-java-format" version "0.7.1"
}

apply plugin: 'com.jaredsburrows.checkerframework'

// To upload to Maven Central, see instructions in the file.
apply from: "${buildscript.sourceFile.parent}/gradle/mavencentral.gradle"

repositories {
  mavenCentral()
}

dependencies {
  testImplementation 'junit:junit:4.12'
  errorprone 'com.google.errorprone:error_prone_core:2.3.1'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

checkerFramework {
  checkers = [
    'org.checkerframework.checker.nullness.NullnessChecker'
  ]
}


/// Javadoc

// Turn Javadoc warnings into errors.
javadoc {
  options.addStringOption('Xwerror', '-Xdoclint:all')
  options.addStringOption('private', '-quiet')
}

task javadocWeb(type: Javadoc) {
  source = sourceSets.main.allJava
  destinationDir = file("/cse/web/research/plumelib/reflection-util/api")
  classpath = project.sourceSets.main.compileClasspath
}

configurations {
  requireJavadoc  // ensure that all Java elements have Javadoc documentation.
}
dependencies {
  implementation group: 'org.plumelib', name: 'require-javadoc', version: '0.1.0'
}
task requireJavadoc(type: Javadoc) {
  description = 'Ensures that Javadoc documentation exists.'
  destinationDir.deleteDir()
  source = sourceSets.main.allJava
  classpath = project.sourceSets.main.compileClasspath
  options.memberLevel = JavadocMemberLevel.PRIVATE
  options.docletpath = project.sourceSets.main.compileClasspath as List
  options.doclet = "org.plumelib.javadoc.RequireJavadoc"
}


/// Emacs support

/* Make Emacs TAGS table */
task tags(type: Exec) {
  description "Run etags to create an Emacs TAGS table"
  commandLine "bash", "-c", "find src/ -name '*.java' | sort | xargs etags"
}
