// To make a release:
//  * Update the version number in ../README.md and in this file.
//  * Update CHANGELOG.md.
//  * Run in the top-level directory:  ./gradlew clean uploadArchives && ./gradlew javadocWeb
//  * Browse to https://oss.sonatype.org/#stagingRepositories, complete the Maven Central release
//  * Commit new version number, then add a git tag:
//    # Stage changes
//    git commit -m "Version 0.0.2" && git tag -a v0.0.2 -m "Version 0.0.2"
//  * Make a GitHub release. Go to the GitHub releases page, make a release, attach the .jar file from build/libs/
// Your ~/.gradle/gradle.properties file must contain:
//   ossrhUsername=YOUR_USER_NAME_HERE
//   ossrhPassword=YOUR_PASSWORD_HERE

///////////////////////////////////////////////////////////////////////////
/// Maven Central publishing
/// (From http://central.sonatype.org/pages/gradle.html )

apply plugin: 'maven'
apply plugin: 'signing'

group = "org.plumelib"
archivesBaseName = "reflection-util"
version = "0.0.2"

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { project.gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
  doFirst {
    repositories {
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        pom.project {
          name 'Reflection-util'
          packaging 'jar'
          description 'Convert among string representations of Java types.'
          url 'https://github.com/plume-lib/reflection-util'

          scm {
            connection 'scm:git:git@github.com:plume-lib/reflection-util.git'
            developerConnection 'scm:git:git@github.com:plume-lib/reflection-util.git'
            url 'git@github.com:plume-lib/reflection-util.git'
          }

          licenses {
            license {
              name 'MIT License'
              url 'https://opensource.org/licenses/MIT'
            }
          }

          developers {
            developer {
              id 'mernst'
              name 'Michael Ernst'
              email 'mernst@alum.mit.edu'
            }
          }
        }
      }
    }
  }
}
